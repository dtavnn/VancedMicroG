/*
 * SPDX-FileCopyrightText: 2013, microG Project Team
 * SPDX-License-Identifier: Apache-2.0
 */

buildscript {
    ext.kotlinVersion = '1.7.20'
    ext.coroutineVersion = '1.6.4'

    ext.annotationVersion = '1.5.0'
    ext.appcompatVersion = '1.5.1'
    ext.coreVersion = '1.9.0'
    ext.collectionVersion = '1.3.0-dev01'
    ext.fragmentVersion = '1.5.5'
    ext.lifecycleVersion = '2.5.1'
    ext.materialVersion = '1.7.0'
    ext.navigationVersion = '2.5.3'
    ext.preferenceVersion = '1.2.0'
    ext.webkitVersion = '1.5.0'

    ext.circleimageviewVersion = '3.1.0'
    ext.volleyVersion = '1.2.1'
    ext.wireVersion = '4.4.3'


    ext.androidBuildGradleVersion = '7.3.1'
    ext.androidBuildVersionTools = '33.0.1'

    ext.androidMinSdk = 23
    ext.androidTargetSdk = 29
    ext.androidCompileSdk = 33

    repositories {
        mavenCentral()
        google()
    }

    dependencies {
        classpath "com.android.tools.build:gradle:$androidBuildGradleVersion"
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlinVersion"
        classpath "com.squareup.wire:wire-gradle-plugin:$wireVersion"
    }
}


def gmsVersion = "22.50.14"
def gmsVersionCode = Integer.parseInt(gmsVersion.replaceAll('\\.', ''))

def ourVersionBase = "0.2.26"
def ourVersionMinor = 52

def ourVersionCode = gmsVersionCode * 1000 + ourVersionMinor
def ourVersionName = "$ourVersionBase.$gmsVersionCode"

allprojects {
    apply plugin: 'idea'

    group = 'org.microg.gms'
    version = ourVersionName
    ext.appVersionCode = ourVersionCode
    ext.isReleaseVersion = false
}

subprojects {
    repositories {
        mavenCentral()
        google()
    }
}